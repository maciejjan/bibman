#!/usr/bin/perl

# This file is part of Bibman -- a console tool for managing BibTeX files.
# Copyright 2017-2018, Maciej Sumalvico <macjan@o2.pl>

# Bibman is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Bibman is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with Bibman. If not, see <http://www.gnu.org/licenses/>.

use strict;
use warnings;
use feature 'unicode_strings';
use Curses;
use Pod::Man;
use Pod::Usage;
use FindBin qw($Bin);
use lib "$Bin/.";

use Bibman::MainScreen;
use Bibman::StatusBar;

use constant VERSION => "0.2.1-alpha";

my $INPUT_FILE;
my $arg = shift @ARGV;
while (defined($arg)) {
  if (($arg eq "-h") || ($arg eq "--help")) {
    print_help();
  } elsif (($arg eq "-v") || ($arg eq "--version")) {
    print_version();
  } elsif ($arg eq "--man") {
    print_manpage();
  } else {
    $INPUT_FILE = $arg;
    last;
  }
  $arg = shift @ARGV;
}

if (!defined($INPUT_FILE)) {
  print "ERROR: no input file supplied.\n\n";
  print_help();
} elsif (!-f $INPUT_FILE) {
  print "ERROR: file $INPUT_FILE does not exist.\n\n";
  print_help();
}

# disable printing STDERR to the terminal (doesn't play well with curses)
if (-t STDERR) {
  use File::Spec;
  open STDERR, '>', File::Spec->devnull() or die "could not open STDERR: $!\n";
}

my $win = new Curses;
initscr;
raw;
keypad($win, 1);
noecho();
curs_set(0);

start_color;
use_default_colors;
init_pair(StatusBar::INFO, COLOR_GREEN, -1);
init_pair(StatusBar::ERROR, COLOR_RED, -1);

my $main = new MainScreen();
$main->{cmdinterp}->execute("open $INPUT_FILE");
$main->show($win);

endwin;

sub print_help {
  pod2usage({ -noperldoc => 1, -verbose => 99, -sections => "SYNOPSIS|OPTIONS" });
}

sub print_version {
  print "bibman " . VERSION . "\n";
  exit 0;
}

sub print_manpage {
  my $parser = Pod::Man->new(release => VERSION, section => 1);
  $parser->parse_file($0);
  exit 0;
}

=head1 NAME

bibman - a console tool for managing BibTeX files

=head1 SYNOPSIS

bibman [options] FILE

=head1 OPTIONS

 -h, --help                print help and exit
 -v, --version             print version information and exit
 --man                     print the manual page and exit

=head1 DESCRIPTION

Bibman (BIBliography MANager) is a small console tool for managing BibTeX
files. Its functionality is meant to be similar to JabRef and its user
interface is inspired by Vim. It enables quick searching, filtering, editing,
sorting etc. of bibliography entries using a text-mode, keyboard-only
interface. It stores bibliographies in plain BibTeX files and uses only
standard fields.

=head1 COMMANDS AND KEYBINDINGS

Bibman can be controlled by typing commands into the command line (like e.g. in
Vim). The command line is triggered by pressing B<:> (colon). Most commands are
bound to a single keyboard key. If a command requires additional parameters,
pressing the key opens the command line, so that parameters can be provided.
The commands and keybindings are listed below.

=head2 Navigation

 k, :go-up             go one entry up
 j, :go-down           go one entry down
 g, :go-to-first       go to first entry
 G, :go-to-last        go to last entry
 o, :open-entry        open the pdf file associated with the entry
 z, :center            center the view at the current entry
 q, :quit              quit bibman (without saving changes)

=head2 Manipulating entries

 a, :add                add new entry
 e, :edit               edit entry
 d, :delete             delete entry
 u, :undo               undo last operation
 -, :move-up            move entry up
 +, :move-down          move entry down

=head2 Searching and filtering

 /, :search [PATTERN]          search for PATTERN in the visible columns of the list
 /, :search [FIELD] [PATTERN]  search for PATTERN in FIELD
 n, :search-next               jump to the next search result
 N, :search-prev               jump to the previous search result
 f, :filter [PATTERN]          show only list items matching PATTERN
 f, :filter [FIELD] [PATTERN]  show only entries, whose FIELD matches PATTERN

=head2 Input/Output

 O, :open [FILENAME]    open a bibliography file
 s, :save               save the entry to the current file
 S, :save [FILENAME]    save the entry to file FILENAME

=head2 Edit screen

 <Up>, k           go one field up
 <Down>, j         go one field down
 <Enter>           toggle editing the field
 q                 save changes and quit the edit screen

=head2 Text input mode

 <Tab>             trigger autocompletion


=head1 SEARCHING AND FILTERING

The commands B<search> and B<filter> can be executed either with a single
argument PATTERN, like:

 :search Smith

or with two arguments: FIELD and PATTERN, like:

 :search author Smith

The first variant will search for matches in the visible elements of the list
(e.g. in case of 3 or more authors, only the first author). The second variant
will search the relevant BibTeX fields (e.g. all authors for the field
"author"). In both cases, PATTERN is a Perl regular expression. For example,
the following command will show only entries published between 2010 and 2015:

 :filter year 201[0-5]

In order to clear the filtering and show all entries, execute the "filter"
command without any arguments.


=head1 AUTOCOMPLETION

While editing certain text fields, the <Tab> key triggers autocompletion.
Currently, this functionality is implemented for the command line, entry types
and author names.

=head1 ENVIRONMENT VARIABLES

 BIBMAN_VIEWER        the program used to open pdf files (default: xdg-open)
 

=head1 LICENSE

Bibman is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Bibman is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Bibman. If not, see <http://www.gnu.org/licenses/>.

=head1 BUGS

Please report bugs to: <http://gitlab.com/mmj/bibman>

=cut
